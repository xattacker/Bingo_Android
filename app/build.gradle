apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if (System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH') != null) {
    apply plugin: 'com.github.triplet.play'
}

android {
    compileSdk 33

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId 'com.xattacker.android.bingo'
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 15
        versionName '1.3.1'

       testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    if (System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS') != null) {
        signingConfigs {
            config {
                keyAlias System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS')
                keyPassword System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_PASSWORD')
                storeFile file(System.getenv('PRIVATE_ANDROID_KEY_STORE_PATH'))
                storePassword System.getenv('PRIVATE_ANDROID_KEY_STORE_STORE_PASSWORD')
                v2SigningEnabled false
            }
        }
    }
    
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles'proguard.cfg'
            
            if (System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS') != null) {
                 signingConfig signingConfigs.config
            }
            
            applicationVariants.all {
                variant ->
                    variant.outputs.each {
                        output ->
                            output.outputFileName = new File(
                                    "Bingo(" + variant.versionName + ").apk"
                            )
                    }
            }
        }
    }


    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    productFlavors {
    }

    namespace 'com.xattacker.android.bingo'
}

if (System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH') != null) {
    play {
        serviceAccountCredentials = file(System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH'))
        track = 'production'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    testImplementation 'org.mockito:mockito-core:2.8.9'

    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}
