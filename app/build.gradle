apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

if (System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH') != null) {
    apply plugin: 'com.github.triplet.play'
}

android {
    compileSdkVersion 29

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId 'com.xattacker.android.bingo'
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 11
        versionName '1.3.0'
    }
    
    if (System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS') != null) {
        signingConfigs {
            config {
                keyAlias System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS')
                keyPassword System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_PASSWORD')
                storeFile file(System.getenv('PRIVATE_ANDROID_KEY_STORE_PATH'))
                storePassword System.getenv('PRIVATE_ANDROID_KEY_STORE_STORE_PASSWORD')
                v2SigningEnabled false
            }
        }
    }
    
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles'proguard.cfg'
            
            if (System.getenv('PRIVATE_ANDROID_KEY_STORE_KEY_ALIAS') != null) {
                 signingConfig signingConfigs.config
            }
            
            applicationVariants.all {
                variant ->
                    variant.outputs.each {
                        output ->
                            output.outputFileName = new File(
                                    "Bingo(" + variant.versionName + ").apk"
                            )
                    }
            }
        }
    }

    productFlavors {
    }
}

if (System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH') != null) {
    play {
        serviceAccountCredentials = file(System.getenv('PRIVATE_GOOGLE_PLAY_PUBLISH_API_KEY_PATH'))
        track = 'production'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.2.0'

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}
